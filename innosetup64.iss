; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Webcad Client Signer"
#define MyAppVersion "2.3"
#define MyAppPublisher "Webcad"
#define MyAppURL "https://webcad.com.tr"
#define MyAppFilePath "ega/EgaClientSigner.jar"
#define MyAppIcoName "ega.ico"
#define AppId "{7C8AFDFA-3ECB-4C7E-B2EE-75A51F678852}"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{#AppId}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={usercf}\{#MyAppName}
DisableDirPage=yes
DisableProgramGroupPage=yes
OutputDir=.
OutputBaseFilename=WebcadClientSigner-setup-x64
Compression=lzma
SolidCompression=yes
PrivilegesRequired=lowest

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "turkish"; MessagesFile: "compiler:Languages\Turkish.isl"

[CustomMessages]
english.OldInstallDetected=An older version of {#MyAppName} is already installed. Do you want to uninstall it?
turkish.OldInstallDetected=Uygulamanın yüklü eski bir sürümü tespit edildi. Kaldırmak istiyor musunuz?

[UninstallRun]
Filename: "{cmd}"; Parameters: "/C taskkill /im javaw.exe /f /t"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}";

[InstallDelete]
Name: "{%USERPROFILE}\EgaClientSigner\NewVersion"; Type: filesandordirs
Name: "{userstartup}\EGAClientSigner.bat"; Type: files
Name: "{commonstartup}\EGAClientSigner.bat"; Type: files
Name: "{userdesktop}\EgaClientSigner.lnk"; Type: files
Name: "{commondesktop}\EgaClientSigner.lnk"; Type: files

[Files]
Source: "ega\*"; DestDir: "{app}\ega"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "jre64\*"; DestDir: "{app}\jre"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "ega\EcsSettings.properties"; DestDir: "{%USERPROFILE}\EgaClientSigner\"; Flags: ignoreversion recursesubdirs createallsubdirs

[Dirs]
Name: "{%USERPROFILE}\EgaClientSigner\NewVersion"; Flags: uninsneveruninstall
Name: "{%USERPROFILE}\EgaClientSigner\NewVersion\EgaClientSigner_lib"; Flags: uninsneveruninstall
Name: "{%USERPROFILE}\EgaClientSigner\Backup"; Flags: uninsneveruninstall

[Icons]
Name: "{app}\{#MyAppName}"; Filename: "{app}\run.bat"; IconFilename: "{app}\ega\EgaClientSigner_lib\{#MyAppIcoName}"
Name: "{userdesktop}\{#MyAppName}"; Filename: "{app}\run.bat"; IconFilename: "{app}\ega\EgaClientSigner_lib\{#MyAppIcoName}"
Name: "{userstartup}\{#MyAppName}"; Filename: "{app}\run.bat"; 
Name: "{userdesktop}\{#MyAppName}"; Filename: "{app}\run.bat"; Tasks: desktopicon; IconFilename: "{app}\ega\EgaClientSigner_lib\{#MyAppIcoName}"

[Run]
Filename: "{app}\run.bat"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
function CreateBatch(): boolean;
var
  fileName : string;
  appPath : string;
  lines : TArrayOfString;
begin
  Result := true;
  appPath := ExpandConstant('{app}')
  fileName := appPath + '\run.bat';
  SetArrayLength(lines, 6);
  lines[0] := 'if exist "%userprofile%\EgaClientSigner\NewVersion\EGAClientSigner.jar" (copy /Y "' + appPath + '\ega\EGAClientSigner.jar" "%userprofile%\EgaClientSigner\Backup\EGAClientSigner.jar_20200507"';
  lines[1] := ' copy /Y "%userprofile%\EgaClientSigner\NewVersion\EGAClientSigner.jar" "' + appPath + '\ega\"';
  lines[2] := ' del "%userprofile%\EgaClientSigner\NewVersion\EGAClientSigner.jar")';
  lines[3] := 'if exist "%userprofile%\EgaClientSigner\NewVersion\EgaClientSigner_lib\*.jar" (copy /Y "%userprofile%\EgaClientSigner\NewVersion\EgaClientSigner_lib\*.jar" "' + appPath + '\ega\EGAClientSigner_lib\"';
  lines[4] := ' del "%userprofile%\EgaClientSigner\NewVersion\EgaClientSigner_lib\*.jar")';
  lines[5] := 'start "WebcadClientSigner" "' + appPath + '\jre\bin\javaw.exe" -cp "' + appPath + '\ega\EGAClientSigner.jar";"' + appPath + '\ega\EgaClientSigner_lib/*";. ecscommon.OpenApplet';
  Result := SaveStringsToFile(filename,lines,false);
  exit;
end;

function GetUninstallString: string;
var
  sUnInstPath: string;
  sUnInstallString: String;
begin
  Result := '';
  sUnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{{#AppId}_is1'); { Your App GUID/ID }
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;

function InitializeSetup: Boolean;
var
  V: Integer;
  iResultCode: Integer;
  sUnInstallString: string;
begin
  Result := True; { in case when no previous version is found }
  if RegValueExists(HKEY_LOCAL_MACHINE,'Software\Microsoft\Windows\CurrentVersion\Uninstall\{#AppId}_is1', 'UninstallString') or RegValueExists(HKEY_CURRENT_USER,'Software\Microsoft\Windows\CurrentVersion\Uninstall\{#AppId}_is1', 'UninstallString') then
  begin
    V := MsgBox(ExpandConstant('{cm:OldInstallDetected}'), mbInformation, MB_YESNO);
    if V = IDYES then
    begin
      Exec('cmd', '/C taskkill /im javaw.exe /f /t', '', SW_SHOW, ewWaitUntilTerminated, iResultCode);
      sUnInstallString := GetUninstallString();
      sUnInstallString := RemoveQuotes(sUnInstallString);
      Exec(ExpandConstant(sUnInstallString), '', '', SW_SHOW, ewWaitUntilTerminated, iResultCode);
      Result := True;
    end
    else
      Result := False;
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if  CurStep=ssPostInstall then
    begin
         CreateBatch();
    end
end;
